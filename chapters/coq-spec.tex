% \setchapterpreamble[u]{\margintoc}
\chapter{A specification of \Coq}
\labch{coq-spec}

In \nrefsec{coq-theory} I already talked about the type theory of \Coq we call
\acrshort{PCUIC} while in \nrefch{formalisation} you caught a glimpse of its
representation inside \Coq itself. In this chapter I will recall a few things
focusing on the points that were not discussed in those chapters like
representation of fixed-points and inductive types, as well as typing.

All this is the subject of~\sidecite{sozeau2019coq}.

\section{Syntax of \acrshort{PCUIC}}

In \MetaCoq, the syntax of \acrshort{PCUIC} is defined as the following
inductive type.
\begin{minted}{coq}
Inductive term :=
| tRel (n : nat)
| tSort (u : Universe.t)
| tProd (na : name) (A B : term)
| tLambda (na : name) (A t : term)
| tLetIn (na : name) (b B t : term)
| tApp (u v : term)
| tConst (k : kername) (ui : Instance.t)
| tInd (ind : inductive) (ui : Instance.t)
| tConstruct (ind : inductive) (n : nat) (ui : Instance.t)
| tCase
    (indn : inductive * nat)
    (p c : term)
    (brs : list (nat * term))
| tProj (p : projection) (c : term)
| tFix (mfix : mfixpoint term) (idx : nat)
| tCoFix (mfix : mfixpoint term) (idx : nat).
\end{minted}

I won't present again variables, \(\Pi\)-types, etc. and will focus on
the other constructors, for this I need to introduce the global environment.
% \mintinline{coq}{tConst}, \mintinline{coq}{tInd}, \mintinline{coq}{tConstruct},
% \mintinline{coq}{tCase}, \mintinline{coq}{tProj}, \mintinline{coq}{tFix}
% and \mintinline{coq}{tCoFix}.

\paradot{Global environment}

Just as for the translation presented in \arefpart{elim-reflection}, we have
a global environment \(\Sigma\) containing the axioms, definitions and
declarations of inductive types. Instead of using de Bruijn indices we use
strings to refer to these declarations. In fact, \mintinline{coq}{kername}
is simply a notation for \mintinline{coq}{string} which we use to lift
ambiguity as to its purpose.


\paradot{Universes}

Besides the global environment we have a set of universes with their
constraints. Indeed, although we usually present the hierarchy of universes in
\Coq using \(\Type_i\) with \(i \in \mathbb{N}\), the implementation is a bit
different in that universes aren't fixed but floating.
\marginnote[0.65cm]{
  In \Coq, \(\Type_i\) is written \mintinline{coq}{Type@{i}}.
}
\begin{minted}{coq}
Type@{i} : Type@{j}
\end{minted}
holds in \Coq as long as the constraint \(i < j\) is satisfied but neither \(i\)
nor \(j\) correspond to natural numbers. Satisfying the constraints means that
there exist a valuation in the natural numbers preserving the constraints.
We also have local universes and constraints for polymorphic definitions;
universe instances (of type \mintinline{coq}{Instance.t}) are there to
substitute those for other universes.
Aside from \mintinline{coq}{Type}, we also represent \mintinline{coq}{Set}
and \mintinline{coq}{Prop}. We do not deal with \mintinline{coq}{SProp} yet.

\paradot{Constants}

\mintinline{coq}{tConst k ui} represents a definition or axiom declared in the
environment under name \mintinline{coq}{k}. Constant declarations are
inhabitants of the following record type.
\begin{minted}{coq}
Record constant_body := {
  cst_type : term ;
  cst_body : option term ;
  cst_universes : universes_decl
}.
\end{minted}
A constant comes with its type and an optional definition. Constants without
definitions are simply axioms.
The \mintinline{coq}{universes_decl} is there in case the constant happens to
be universe polymorphic.

\paradot{Inductive types}

\mintinline{coq}{tInd ind ui} represents an inductive type defined in the global
context. The type inductive is
\begin{minted}{coq}
Record inductive : Set := mkInd {
  inductive_mind : kername ;
  inductive_ind : nat
}.
\end{minted}
It is not just a \mintinline{coq}{kername} because inductive types can be
mutually defined: the kername points to the mutual block,
\mintinline{coq}{inductive_ind} points to one inductive in the block.
Note that this term refers to the inductive type, without its parameters or
indices. For vectors this would be
\begin{minted}{coq}
vec : Type -> nat -> Type
\end{minted}
In the global environment an inductive declaration provides the following
information.
\begin{minted}{coq}
Record mutual_inductive_body := {
  ind_finite    : recursivity_kind ;
  ind_npars     : nat ;
  ind_params    : context ;
  ind_bodies    : list one_inductive_body ;
  ind_universes : universes_decl ;
  ind_variance  : option (list Universes.Variance.t)
}.
\end{minted}
These informations include the number of parameters, the type of these
parameters in the form of a context and the universe declarations.
\mintinline{coq}{ind_variance} deals with variance of universes in the case
of cumulative inductive types.
The \mintinline{coq}{recursivity_kind} determines what kind of inductive type
we are building, this in fact include the usual inductive types, but also
record types and coinductive types.
Finally, \mintinline{coq}{ind_bodies} is a list of declarations for each of
the inductive types in the mutual block.
\begin{minted}{coq}
Record one_inductive_body := {
  ind_name  : ident ;
  ind_type  : term ;
  ind_kelim : sort_family ;
  ind_ctors : list (ident * term * nat) ;
  ind_projs : list (ident * term)
}.
\end{minted}
Each of those comes with its name and type.
\mintinline{coq}{ind_kelim} states in which univeres the definition can be
eliminated (in most cases inductive types living in \mintinline{coq}{Prop}
cannot be scrutinised to build terms in \mintinline{coq}{Type}).
Then we either have a list of constructors for (co)inductive types, or a list
of projections for (negative) records.

\paradot{Constructors}

\mintinline{coq}{tConstruct ind n ui} comes with \mintinline{coq}{ind} to point
to the right inductive type while \mintinline{coq}{n} tells us which constructor
it is.
For instance it would be \mintinline{coq}{0} for \mintinline{coq}{true} and
\mintinline{coq}{1} for \mintinline{coq}{false}.
Each constructor is given by a term of type
\begin{minted}{coq}
ident * term * nat
\end{minted}
as we saw in the previous paragraph. It consists in a name, a type and an arity.

\paradot{Pattern-matching}
\mintinline{coq}{tCase (ind, n) p c brs} is the representation of the
pattern-matching on \mintinline{coq}{c} of inductive type \mintinline{coq}{ind}
with \mintinline{coq}{n} parameters, return predicate \mintinline{coq}{p}
and branches \mintinline{coq}{brs}.
There is one branch for each constructor and it contains the arity of the
constructor and the term corresponding to the branch.

\paradot{Projections}
\mintinline{coq}{tProj p c} represents the projection \mintinline{coq}{p}
applied to term \mintinline{coq}{c}.
\begin{minted}{coq}
Definition projection :=
  inductive * nat * nat.
\end{minted}
A projection is described by the inductive (or rather record) to which it
belongs, the number of parameters of the record, and the index of the projected
argument. In the case of dependent pairs, the latter will be \mintinline{coq}{0}
for the first projection and \mintinline{coq}{1} for the second.

\paradot{Fixed-points}
Finally we have \mintinline{coq}{tFix mfix idx} representing fixed-points and
\mintinline{coq}{tCoFix mfix idx} for cofixed-points.
Like inductive types, fixed-points can be defined mutually, as such
\mintinline{coq}{mfix} is a list of (mutual) definitions:
\begin{minted}{coq}
Definition mfixpoint term :=
  list (def term).
\end{minted}
while \mintinline{coq}{idx} tells which one we are referring to.
Definitions which are given as
\begin{minted}{coq}
Record def term := mkdef {
  dname : name ;
  dtype : term ;
  dbody : term ;
  rarg  : nat
}.
\end{minted}
that is with a name, a type and a body (the definition itself).
The \mintinline{coq}{rarg} field points to the recursive argument of the
fixed-point.
For instance in
\begin{minted}{coq}
Fixpoint map {A B : Type} (f : A -> B) (l : list A) {struct l}
  : list B :=
  match l with
  | [] => []
  | x :: l => f x :: map f l
  end.
\end{minted}
the \mintinline{coq}{{struct l}} means that \mintinline{coq}{l} is the recursive
argument. That is all recursive calls must be structually decreasing on
\mintinline{coq}{l}. It is part of the syntax because \Coq uses it as a
syntactic guard to avoid unfolding fixed-points indefinitely: only when the
recursive argument is a constructor can the fixed-point be unfolded.

\paradot{Local environments}
Local environments or contexts are lists of declarations we write in \emph{snoc}
order:
\begin{itemize}
  \item \mintinline{coq}{[]} is the empty context;
  \item \mintinline{coq}{Γ ,, vass na A} extends \mintinline{coq}{Γ} with
  an \emph{assumption} variable of type \mintinline{coq}{A}, named
  \mintinline{coq}{na};
  \item \mintinline{coq}{Γ ,, vdef na a A} extends \mintinline{coq}{Γ} with
  a \emph{local definition} \mintinline{coq}{a} of type \mintinline{coq}{A},
  named \mintinline{coq}{na}.
\end{itemize}

Now that the syntax is out of the way we can move to the semantics.

\section{Semantics of \acrshort{PCUIC}}

There are two important properties defined on the syntax: reduction and typing.
The former being necessary for the latter as it is the base for conversion.

\subsection{Reduction}

We define reduction as a relation on terms, however, unlike for \acrshort{STL}
or many systems, because of constants and let-bindings which can be unfolded
to their definitions, the relation has to mention the global and local
environment. Note also that the relation is not functional, that is that the
reduction we define is not deterministic.

The inductive type of \(1\)-step reduction is
\begin{minted}{coq}
Inductive red1 (Σ : global_env) (Γ : context) :
  term -> term -> Type
\end{minted}
I will give the different constructors one by one, while explaining them,
focusing on the computation rules.

\paradot{\(\beta\)-reduction}
\begin{minted}{coq}
red_beta na t b a :
  red1 Σ Γ (tApp (tLambda na t b) a) (subst10 a b)
\end{minted}
This is the usual \(\beta\)-reduction rule, \mintinline{coq}{subst10 a b}
is just the term \mintinline{coq}{b} where the \(0\)-th variable is substituted
by \mintinline{coq}{a}.

\paradot{\(\zeta\)-reduction}
\begin{minted}{coq}
red_zeta na b t b' :
  red1 Σ Γ (tLetIn na b t b') (subst10 b b')
\end{minted}
This rule is similar to \(\beta\)-reduction, it states that the expression
\begin{minted}{coq}
let x := b in t x
\end{minted}
reduces to \mintinline{coq}{t b}.

\paradot{Local definition unfolding}
\begin{minted}{coq}
red_rel i body :
  option_map decl_body (nth_error Γ i) = Some (Some body) ->
  red1 Σ Γ (tRel i) (lift0 (S i) body)
\end{minted}
If the context contains some local definition \(x := t\) then the variable
\(x\) \emph{reduces} to \(t\). Because the definition makes sense in a
smaller context, it needs to be weakened, hence the \mintinline{coq}{lift}.

\paradot{\(\iota\)-reduction}
\marginnote[1.5cm]{
  \mintinline{coq}{mkApps} is a shortcut to apply a term to a list of arguments.
}
\begin{minted}{coq}
red_iota ind pars c u args p brs :
  red1 Σ Γ
    (tCase (ind, pars) p (mkApps (tConstruct ind c u) args) brs)
    (iota_red pars c args brs)
\end{minted}
Pattern-matching reduces when the term under scrutiny is a constructor
(potentially applied to some arguments).
\mintinline{coq}{iota_red} is defined as follows
\begin{minted}{coq}
Definition iota_red npar c args brs :=
  mkApps
    (snd (List.nth c brs (0, tDummy)))
    (List.skipn npar args).
\end{minted}
\mintinline{coq}{List.nth} takes a default value, so
\mintinline{coq}{(0, tDummy)} can be safely ignored. \mintinline{coq}{iota_red}
basically picks the branch corresponding to the constructor giving it the list
of indices \mintinline{coq}{List.skipn npar args}.

\paradot{Fixed-point unfolding}
\begin{minted}{coq}
red_fix mfix idx args narg fn :
  unfold_fix mfix idx = Some (narg, fn) ->
  is_constructor narg args = true ->
  red1 Σ Γ (mkApps (tFix mfix idx) args) (mkApps fn args)
\end{minted}
Here we can witness the syntactic guard I talked about when describing
fixed-points: when a fixed-point is applied to arguments such that its recursive
argument is an applied constructor, it can safely be unfolded.
\mintinline{coq}{unfold_fix mfix idx} returns the recursive argument
\mintinline{coq}{narg} and the body of the fixed-point \mintinline{coq}{fn}.

\paradot{Cofixed-point unfolding}
There are two rules to deal with unfolding of cofixed-points: one when it is
pattern-matched, the other when it is projected.
\begin{minted}{coq}
red_cofix_case ip p mfix idx args narg fn brs :
  unfold_cofix mfix idx = Some (narg, fn) ->
  red1 Σ Γ
    (tCase ip p (mkApps (tCoFix mfix idx) args) brs)
    (tCase ip p (mkApps fn args) brs)

red_cofix_proj p mfix idx args narg fn :
  unfold_cofix mfix idx = Some (narg, fn) ->
  red1 Σ Γ
    (tProj p (mkApps (tCoFix mfix idx) args))
    (tProj p (mkApps fn args))
\end{minted}
The are very similar to fixed-points except this time there is no syntactic
guard with respect to a recursive argument.

\paradot{\(\delta\)-reduction}
\begin{minted}{coq}
red_delta c decl body (isdecl : declared_constant Σ c decl) u :
  decl.(cst_body) = Some body ->
  red1 Σ Γ (tConst c u) (subst_instance_constr u body)
\end{minted}
A constant can reduce to its definition in the global environment (if it has
one, \ie if it is not an axiom). Since the definition is potentially universe
polymorphic, we instantiate its universes with the one the constant was used
with.

\paradot{Projection}
\begin{minted}{coq}
red_proj i pars narg args k u arg:
  nth_error args (pars + narg) = Some arg ->
  red1 Σ Γ
    (tProj (i, pars, narg) (mkApps (tConstruct i k u) args))
    arg
\end{minted}
When a constructor of a record (that is the record is given with its fields) is
projected, it reduces to the corresponding field.

\paradot{Congruence rules}
All remaining rules are congruence rules we have for instance the two congruence
rules for \mintinline{coq}{tLambda}:
\begin{minted}{coq}
| abs_red_l na M M' N :
    red1 Σ Γ M M' ->
    red1 Σ Γ (tLambda na M N) (tLambda na M' N)

| abs_red_r na M M' N :
    red1 Σ (Γ ,, vass na N) M M' ->
    red1 Σ Γ (tLambda na N M) (tLambda na N M')
\end{minted}
They state that you can reduce either subterm.

\subsection{Conversion}

Unlike \Agda, conversion in \Coq isn't typed but rather based primarily on
reduction. Its definition is the following, corresponding basically to the
reflexive, symmetric and transitive closure of reduction:
\marginnote[1cm]{
  Here \mintinline{coq}{Σ} is a pair comprised of a global environment and the
  universe declarations.
}
\begin{minted}{coq}
Inductive conv Σ Γ : term -> term -> Type :=
| conv_refl t u :
    eq_term (global_ext_constraints Σ) t u ->
    Σ ;;; Γ |- t = u

| conv_red_l t u v :
    red1 Σ Γ t v ->
    Σ ;;; Γ |- v = u ->
    Σ ;;; Γ |- t = u

| conv_red_r t u v :
    Σ ;;; Γ |- t = v ->
    red1 (fst Σ) Γ u v ->
    Σ ;;; Γ |- t = u

where " Σ ;;; Γ |- t = u " := (@conv _ Σ Γ t u) : type_scope.
\end{minted}

In terms of inference rules this would be written
\begin{mathpar}
  \infer
    {t =_{\alpha} u}
    {\Sigma ; \Ga \vdash t \equiv u}
  %

  \infer
    {
      \Sigma ; \Ga \vdash t \red v \\
      \Sigma ; \Ga \vdash v \equiv u
    }
    {\Sigma ; \Ga \vdash t \equiv u}
  %

  \infer
    {
      \Sigma ; \Ga \vdash t \equiv v \\
      \Sigma ; \Ga \vdash u \red v
    }
    {\Sigma ; \Ga \vdash t \equiv u}
  %
\end{mathpar}
This is similar except that we don't merely use \(\alpha\)-conversion in the
reflexive case, we also equate the terms up to universes.
Indeed sometimes, two universes may be syntactically different but still be the
same like \(i\) and \(j\) with constraints \(i \le j\) and \(j \le i\).

This becomes more apparent in the cumulativity definition.
\begin{minted}{coq}
Inductive cumul Σ Γ : term -> term -> Type :=
| cumul_refl t u :
    leq_term (global_ext_constraints Σ) t u ->
    Σ ;;; Γ |- t <= u

| cumul_red_l t u v :
    red1 Σ Γ t v ->
    Σ ;;; Γ |- v <= u ->
    Σ ;;; Γ |- t <= u

| cumul_red_r t u v :
    Σ ;;; Γ |- t <= v ->
    red1 Σ Γ u v ->
    Σ ;;; Γ |- t <= u

where " Σ ;;; Γ |- t <= u " := (cumul Σ Γ t u) : type_scope.
\end{minted}

\begin{mathpar}
  \infer
    {t \le_{\alpha} u}
    {\Sigma ; \Ga \vdash t \cumul u}
  %

  \infer
    {
      \Sigma ; \Ga \vdash t \red v \\
      \Sigma ; \Ga \vdash v \cumul u
    }
    {\Sigma ; \Ga \vdash t \cumul u}
  %

  \infer
    {
      \Sigma ; \Ga \vdash t \cumul v \\
      \Sigma ; \Ga \vdash u \red v
    }
    {\Sigma ; \Ga \vdash t \cumul u}
  %
\end{mathpar}
Of course this time it is not symmetric. We break symmetry by using
\mintinline{coq}{leq_term} to perform \(\alpha\)-equlity up to universes.
In a sense this is what does all the work regarding cumulativity.

We define \mintinline{coq}{eq_term} and \mintinline{coq}{leq_term} at the same
time using a more general definition.
\marginnote[0.5cm]{
  \mintinline{coq}{φ} represents the universe constraints.
  \mintinline{coq}{eq_universe} and \mintinline{coq}{leq_universe} are relations
  on universes with self-explanatory names.
}
\begin{minted}{coq}
Definition eq_term φ :=
  eq_term_upto_univ (eq_universe φ) (eq_universe φ).

Definition leq_term φ :=
  eq_term_upto_univ (eq_universe φ) (leq_universe φ).
\end{minted}

The definition of \mintinline{coq}{eq_term_upto_univ} is rather long so I will
give an excerpt of it.
\begin{minted}{coq}
Inductive eq_term_upto_univ
  (Re Rle : Universe.t -> Universe.t -> Prop)
  : term -> term -> Type :=

| eq_Rel n  :
    eq_term_upto_univ Re Rle (tRel n) (tRel n)

| eq_Sort s s' :
    Rle s s' ->
    eq_term_upto_univ Re Rle (tSort s) (tSort s')

| eq_App t t' u u' :
    eq_term_upto_univ Re Rle t t' ->
    eq_term_upto_univ Re Re u u' ->
    eq_term_upto_univ Re Rle (tApp t u) (tApp t' u')

| eq_Const c u u' :
    R_universe_instance Re u u' ->
    eq_term_upto_univ Re Rle (tConst c u) (tConst c u')

| eq_Prod na na' a a' b b' :
    eq_term_upto_univ Re Re a a' ->
    eq_term_upto_univ Re Rle b b' ->
    eq_term_upto_univ Re Rle (tProd na a b) (tProd na' a' b')

(* ... *)
.
\end{minted}
As you can see, when comparing sorts we simply compare the universes with
\mintinline{coq}{Rle}. Otherwise, we simply compare the subterms with the same
relations. There is the exception of domains however, indeed they are in
contravariant positions and as such they should need to be compared in reverse
order. In \Coq we simply use conversion rather that cumulativity in
contravariant positions:
\begin{mathpar}
  \infer
    {
      A =_\alpha A' \\
      B \le_\alpha B'
    }
    {\Pi (x:A). B \le_\alpha \Pi (x:A'). B'}
  %
\end{mathpar}
We also compare universe instances for polymorphic constants.

\paradot{About \(\eta\)}
\reminder[-0.7cm]{\(\eta\)-expansion}{
  \(\eta\)-expansion is defined as follows
  \[
    t \red_\eta \lambda x.\ t\ x
  \]
}
\Coq supports \(\eta\)-expansion in the conversion. In a typed setting like
\Agda's conversion it is rather easy to add it. In \Agda the conversion rule can
be given as
\[
  \infer
    { }
    {\Ga \vdash t \equiv \lambda x.\ t\ x : \Pi (x:A). B}
  %
\]
or even in a more directed way as
\[
  \infer
    {\Ga, x:A \vdash f\ x \equiv g\ x : B}
    {\Ga \vdash f \equiv g : \Pi (x:A). B}
  %
\]
In an untyped setting the question is a bit more complex, we have several
candidates but are struggling to find a formulation that lets us preserve good
properties about conversion/cumulativity without going through to much trouble.

\subsection{Typing}

Again, typing is defined inductively.
\begin{minted}{coq}
Inductive typing (Σ : global_env_ext) (Γ : context)
  : term -> term -> Type
(* ... *)
where " Σ ;;; Γ |- t : T " := (typing Σ Γ t T) : type_scope.
\end{minted}
I will go over the rules one by one.

\paradot{Variables}
\begin{minted}{coq}
type_Rel n decl :
  All_local_env (lift_typing typing Σ) Γ ->
  nth_error Γ n = Some decl ->
  Σ ;;; Γ |- tRel n : lift0 (S n) decl.(decl_type)
\end{minted}
Herein \mintinline{coq}{All_local_env (lift_typing typing Σ) Γ} will later on
be written \mintinline{coq}{wf_local Σ Γ}, it corresponds to well-formedness
of the local environment \(\Sigma \vdash \Ga\). This basically means that
the types and local definitions in it are well-typed.
\begin{minted}{coq}
nth_error Γ n = Some decl
\end{minted}
verifies that the variable
\mintinline{coq}{n} corresponds to a declaration in \mintinline{coq}{Γ}.
The type of the variable is thus the type stored in this declaration. As always
it has to be \emph{lifted}, \ie weakened to account for the declarations
that were added successively in \mintinline{coq}{Γ}.

\paradot{Sorts}
\begin{minted}{coq}
type_Sort l :
  wf_local Σ Γ ->
  LevelSet.In l (global_ext_levels Σ) ->
  Σ ;;; Γ |- tSort (Universe.make l) : tSort (Universe.super l)
\end{minted}
For universes we once again require to local environment to be well-formed.
The universe should also make sense in the global environment, in which case it
is typed in its successor universe.

\paradot{\(\Pi\)-types}
\begin{minted}{coq}
type_Prod na A B s1 s2 :
  Σ ;;; Γ |- A : tSort s1 ->
  Σ ;;; Γ ,, vass na A |- B : tSort s2 ->
  Σ ;;; Γ |- tProd na A B : tSort (sort_of_product s1 s2)
\end{minted}
\(\Pi (x:A).B\) is well-typed if \(A\) and \(B\) are well-typed too.
The \(\Pi\)-types lives in another universe computed from the universes of
\(A\) and \(B\), if those are \(\Type_i\) and \(\Type_j\) this will be
\(\Type_{\max\ i\ j}\) but if the second is \(\Prop\), then the \(\Pi\)-type
lives in \(\Prop\) too thanks to impredicativity.

\paradot{\(\lambda\)-abstractions}
\begin{minted}{coq}
type_Lambda na A t s1 B :
  Σ ;;; Γ |- A : tSort s1 ->
  Σ ;;; Γ ,, vass na A |- t : B ->
  Σ ;;; Γ |- tLambda na A t : tProd na A B
\end{minted}

\paradot{let-bindings}
\begin{minted}{coq}
type_LetIn na b B t s1 A :
  Σ ;;; Γ |- B : tSort s1 ->
  Σ ;;; Γ |- b : B ->
  Σ ;;; Γ ,, vdef na b B |- t : A ->
  Σ ;;; Γ |- tLetIn na b B t : tLetIn na b B A
\end{minted}
Here we see the interesting fact that let-bindings are typed by let-bindings
themselves. That is because the type \mintinline{coq}{A} also makes sense in
\mintinline{coq}{Γ ,, vdef na b B} that is an environment extended with a local
definition.

\paradot{Application}
\begin{minted}{coq}
type_App t na A B u :
  Σ ;;; Γ |- t : tProd na A B ->
  Σ ;;; Γ |- u : A ->
  Σ ;;; Γ |- tApp t u : B{0 := u}
\end{minted}
Here \mintinline{coq}{B{0 := u}} stands for \mintinline{coq}{B} where the
\(0\)-th variable is substituted by \mintinline{coq}{u}.

\paradot{Constants}
\begin{minted}{coq}
type_Const k u :
  wf_local Σ Γ ->
  forall d (isdecl : declared_constant Σ.1 k d),
  consistent_instance_ext Σ d.(cst_universes) u ->
  Σ ;;; Γ |- tConst k u : subst_instance_constr u d.(cst_type)
\end{minted}
The rule is a complicated way of saying that if the constant
\mintinline{coq}{k} is declared in the global environment and used with
a consistent instance of universes, it has the type prescribed by
\mintinline{coq}{Σ} instantiated with those universes.

\paradot{Inductive types}
\begin{minted}{coq}
type_Ind ind u :
  wf_local Σ Γ ->
  forall md id (isdecl : declared_inductive Σ.1 md ind id),
  consistent_instance_ext Σ md.(ind_universes) u ->
  Σ ;;; Γ |- tInd ind u : subst_instance_constr u id.(ind_type)
\end{minted}
This rule is pretty similar to constants because without their constructors,
inductive types are really just constants.

\paradot{Constructors}
\begin{minted}{coq}
type_Construct ind i u :
  wf_local Σ Γ ->
  forall md id cd
    (isdecl : declared_constructor Σ.1 md id (ind, i) cd),
    consistent_instance_ext Σ md.(ind_universes) u ->
    Σ ;;; Γ |- tConstruct ind i u :
               type_of_constructor md cd (ind, i) u
\end{minted}
A constructor is well-typed when the inductive it refers to is declared and
the constructor itself is declared in it.
\mintinline{coq}{type_of_constructor} recovers the corresponding type and put it
in context, substituting the universes.

\paradot{Pattern-matching}
\begin{minted}{coq}
type_Case in u p c brs args :
  let ind := in.1 in
  let npar := in.2 in
  forall md id (isdecl : declared_inductive Σ.1 md ind id),
  md.(ind_npars) = npar ->
  let params := List.firstn npar args in
  forall ps pty,
  build_case_predicate_type ind md id params u ps = Some pty ->
  Σ ;;; Γ |- p : pty ->
  leb_sort_family (universe_family ps) id.(ind_kelim) ->
  Σ ;;; Γ |- c : mkApps (tInd ind u) args ->
  forall btys,
  map_option_out (build_branches_type ind md id params u p) =
  Some btys ->
  All2 (fun br bty =>
    (br.1 = bty.1) *
    (Σ ;;; Γ |- br.2 : bty.2) *
    (Σ ;;; Γ |- bty.2 : tSort ps)
  ) brs btys ->
  Σ ;;; Γ |- tCase in p c brs :
             mkApps p (skipn npar args ++ [c])
\end{minted}
This rule is rather hairy. We verify that the inductive type given for the
scrutiny is declared; we verify that the return predicate has the right type and
is in a sort which can be eliminated to; we verify that the scrutiny is indeed
typed in the inductive type and we recover the indices from its type, they will
be used in the return type; finally we verify that each branch is well-typed
using the other information.

\paradot{Projection}
\begin{minted}{coq}
type_Proj p c u :
  forall md id pd
    (isdecl : declared_projection Σ.1 md id p pd) args,
    Σ ;;; Γ |- c : mkApps (tInd (fst (fst p)) u) args ->
    #|args| = ind_npars md ->
    let ty := snd pd in
    Σ ;;; Γ |- tProj p c :
    subst0 (c :: List.rev args) (subst_instance_constr u ty)
\end{minted}
As usual we verify that the projection is declared and corresponds to a declared
record type. The projected term must be of that record type, where universes
are instantited. The projection has then the type of the corresponding field
with these universes.

\paradot{Fixed-points}
\begin{minted}{coq}
type_Fix mfix n decl :
  fix_guard mfix ->
  nth_error mfix n = Some decl ->
  wf_local Σ Γ ->
  All (fun d => {s & Σ ;;; Γ |- d.(dtype) :  tSort s}) mfix ->
  All (fun d =>
    (Σ ;;; Γ ,,, fix_context mfix |-
      d.(dbody) :
      lift0 #|fix_context mfix| d.(dtype)
    ) *
    (isLambda d.(dbody) = true)
  ) mfix ->
  Σ ;;; Γ |- tFix mfix n : decl.(dtype)
\end{minted}
A fixed-point is well-typed if all the types of the different mutual functions
are sorted (\ie are typed in a universe) and if the bodies have their ascribed
types in the context extended with the mutual functions (the bodies can refer
to other functions or do a recusrive call to themselves).
We also ask that each body is at least a \(\lambda\)-abstraction to make sure it
takes at least one argument explicitly.
The interesting point however is
\begin{minted}{coq}
fix_guard mfix
\end{minted}
This is our own representation of the guard condition ensuring that the
fixed-point is terminating. As the guard condition of \Coq is rather complicated
and perhaps ad-hoc we instead assume we have such a guard condition satisfying
some properties and work with it in generality.
For this we use axioms
\begin{minted}{coq}
Axiom fix_guard : mfixpoint term -> bool.

Axiom fix_guard_red1 :
  forall Σ Γ mfix mfix' idx,
    fix_guard mfix ->
    red1 Σ Γ (tFix mfix idx) (tFix mfix' idx) ->
    fix_guard mfix'.

(* ... *)
\end{minted}
which has the drawback that they cannot be instantiated with actual guard
conditions. We will probably change this at some point in the future.

\paradot{Cofixed-points}
\begin{minted}{coq}
type_CoFix mfix n decl :
  allow_cofix ->
  nth_error mfix n = Some decl ->
  wf_local Σ Γ ->
  All (fun d => {s & Σ ;;; Γ |- d.(dtype) :  tSort s}) mfix ->
  All (fun d =>
    Σ ;;; Γ ,,, fix_context mfix |-
      d.(dbody) :
      lift0 #|fix_context mfix| d.(dtype)
  ) mfix ->
  Σ ;;; Γ |- tCoFix mfix n : decl.(dtype)
\end{minted}
Cofixed-points are pretty similar to fixed-points except we don't even check
productivity, so that is still something that we need to work on.

\paradot{Cumulativity}
\begin{minted}{coq}
type_Cumul t A B :
  Σ ;;; Γ |- t : A ->
  (isWfArity typing Σ Γ B + {s & Σ ;;; Γ |- B : tSort s}) ->
  Σ ;;; Γ |- A <= B ->
  Σ ;;; Γ |- t : B
\end{minted}
The cumulativity rule is mostly unsurprising, if \(t : A\) and \(A \cumul B\)
then \(t : B\). As I have showed several times we require \(B\) to be
well-formed because conversion is untyped.
The way it is written here might be surprising however: we ask for the existence
of a sort typing \(B\) \emph{or} that \(B\) is a well-formed \emph{arity}.
This is an oddity of \Coq coming from the presence of so-called
\emph{algebraic universes} consisting of things such as \(i+1\) or
\(\max\ i\ j\). These universes should only appear on the right-hand side of
the colon, \ie in types.
An arity is a well-formed quantification over a universe that may be
algebraic. As those can appear in types, we allow them in the cumulativity
rule.
This peculiar aspect can be mostly ignored for the rest of this document,
and well-formedness of types can be thought of as the usual.


Note that I did not talk about typing of environments, local and global, but
they of course are also specified in the formalisation.
For inductive type declarations there is another guard condition that we
axiomatise:
\begin{minted}{coq}
Axiom ind_guard : mutual_inductive_body -> bool.
\end{minted}
In \Coq this is implemented as the strict positivity condition but we once again
remain abstract and simply ask for some way of determining if an inductive type
is well-founded.
These axioms will become meaningful when we reach the question of strong
normalisation in \nrefch{coq-meta-theory}.