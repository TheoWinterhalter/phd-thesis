% \setchapterpreamble[u]{\margintoc}
\chapter{Formalisation of the translation}
\labch{elim-formalised}

The formalisation is inspired from that of \Coq in \MetaCoq. It is actually
defined \emph{besides} \MetaCoq to allow for some interoperability, bringing
about fairly realistic examples.
This provides evidence that the translation is constructive \emph{and} computes!
Note that we also rely on the
\Equations~\sidecite{DBLP:conf/itp/Sozeau10,sozeau2019equations} plugin to
derive nice dependent induction principles.

Our formalisation takes full advantage of its easy interfacing with \MetaCoq:
we define two theories, namely \acrshort{ETT} and \acrshort{ITT}
(or \acrshort{WTT}), but the target features a lot of syntactic
sugar by having things such as transport, heterogeneous equality and packing as
part of the syntax. The operations regarding these constructors---in particular
the tedious ones---are written in \Coq and then quoted to finally be
\emph{realised} in a translation from \acrshort{ITT} to \MetaCoq.
\marginnote[1cm]{
  Some work for the future\dots
}
For \acrshort{WTT} the story is a bit different because there is no \emph{weak}
\Coq or \MetaCoq so for now there is no translation to a purer \acrshort{WTT}
though this is work that we started but put on hold by coming to realise the
target would not be that much simpler.

\paragraph{Interoperability with \MetaCoq.}
The translation we define from \acrshort{ITT} to \MetaCoq is not proven
correct, but it is not really important as it can just be seen as a
feature to observe the produced terms in a nicer setting.
\marginnote{
  We will discuss about \MetaCoq and its theory in greater detail in
  \arefpart{coq-in-coq}.
}
In any case, \MetaCoq does not yet provide a complete formalisation of
\acrshort{CIC} rules, as guard checking of recursive definitions and strict
positivity of inductive type declarations are not formalised yet.

Our formalised theorems however do not depend on \MetaCoq itself and as such
there is no need to \emph{trust} the plugin or the formalisation of \Coq inside
it.

We also provide a translation from \MetaCoq (and thus \Coq!) to \acrshort{ETT}
that we will describe more extensively with the examples in
\nrefsec{ett-flavoured}.

\section{Quick Overview of the Formalisation}

The formalisation can be found at
\href{https://github.com/TheoWinterhalter/ett-to-itt}{github.com/TheoWinterhalter/ett-to-itt}
and
\href{https://github.com/TheoWinterhalter/ett-to-itt/tree/weak}{github.com/TheoWinterhalter/ett-to-itt/tree/weak}
for the translations from \acrshort{ETT} to \acrshort{ITT} and from
\acrshort{ETT} to \acrshort{WTT} respectively.
Let me describe quickly the formalisation in the case of \acrshort{ETT} to
\acrshort{ITT}, the other formalisation is pretty similar.

\marginnote[0.1cm]{
  In \nrefch{elim-reflection-framework} I showed two different syntaxes but to
  make things simpler I use a common type of terms, only some terms will only
  have a typing rule in \acrshort{ITT}.
}
The file \rpath{SAst.v} contains the definition of the (common) abstract syntax
of \acrshort{ETT} and \acrshort{ITT} in the form of an inductive definition with
de Bruijn indices for variables.
Sorts are defined separately in \rpath{Sorts.v} and we will address them later
in \nrefsec{elim-trans-sorts}.

\begin{minted}{coq}
Inductive sterm : Type :=
| sRel (n : nat)
| sSort (s : sort)
| sProd (nx : name) (A B : sterm)
| sLambda (nx : name) (A B t : sterm)
| sApp (u : sterm) (nx : name) (A B v : sterm)
| sEq (A u v : sterm)
| sRefl (A u : sterm)
| (* ... *) .
\end{minted}

The files \rpath{ITyping.v} and \rpath{XTyping.v} define respectively the
typing judgments for \acrshort{ITT} and \acrshort{ETT}.
The first is defined with first a notion of reduction from which is deduced
conversion, while the latter has typed conversion and typing defined using
mutual inductive types.
\marginnote[0.3cm]{
  In the formalisation of the translation to \acrshort{WTT}, these meta-theory
  files are kept to a minimum!
}
Then, most of the files are focused on the meta-theory of \acrshort{ITT} and can
be ignored by readers who do not need to see yet another proof of subject
reduction.

The most interesting files are obviously those where the fundamental lemma
and the translation are formalised: \rpath{FundamentalLemma.v} and
\rpath{Translation.v}.
For instance, here is the main theorem, as stated in our formalisation:
%
\begin{minted}{coq}
Theorem complete_translation {Σ} :
  type_glob Σ ->
  (forall {Γ t A} (h : Σ ;;; Γ |-x t : A)
     {Γ'} (hΓ : Σ |--i Γ' ∈ ⟦ Γ ⟧),
      ∑ A' t', Σ ;;;; Γ' ⊢ [t'] : A' ∈ ⟦ Γ ⊢ [t] : A ⟧) *
  (forall {Γ u v A} (h : Σ ;;; Γ |-x u ≡ v : A)
     {Γ'} (hΓ : Σ |--i Γ' ∈ ⟦ Γ ⟧),
      ∑ A' A'' u' v' p',
        eqtrans Σ Γ A u v Γ' A' A'' u' v' p').
\end{minted}
%
Herein \mintinline{coq}{type_glob Σ} refers to the fact that the global
context is well-typed---thing which we mainly ignored in the paper translation.
The fact that the theorem holds in \Coq ensures we can actually
compute a translated term and type out of a derivation in \acrshort{ETT}.

%%%%% Seems like this is covered in the translation itself.
% \section{Inductive Types and Recursion}
% \label{sec:inductives}

% In the proof of Section~\ref{sec:translation}, we did not mention
% anything about inductive types, pattern-matching or recursion as it is
% a bit technical on paper.  In the formalisation, we offer a way to
% still be able to use them, and we will even show how it works in
% practice with the examples (Section\ref{sec:examples}).

% The main guiding principle is that inductive types and induction are orthogonal
% to the translation, they should more or less be translated to
% themselves.
% %
% To realise that easily, we just treat an inductive definition as a way
% to introduce new constants in the theory, one for the type, one for
% each constructor, one for its elimination principle, and one equality
% per computation rule.
% %
% For instance, the natural numbers can be represented by having the following
% constants in the context:
% %
% \[
% \begin{array}{l@{~}c@{~}l}
%   \nat &:& \Ty{0} \\
%   \zero &:& \nat \\
%   \natsucc &:& \nat \to \nat \\
%   \natrec &:& \forall P,\
%   P\ \zero \to (\forall m,\ P\ m \to P\ (\natsucc\ m)) \to
%   \forall n,\ P\ n \\
%   \natrec_\zero &:& \forall P\ P_z\ P_s,\ \natrec\ P\ P_z\ P_s\ \zero = P_z \\
%   \natrec_\natsucc &:& \forall P\ P_z\ P_s\ n,\\
%   &&\natrec\ P\ P_z\ P_s\ (\natsucc\ n) = P_s\ n\ (\natrec\ P\ P_z\ P_s\ n)
% \end{array}
% \]
% %
% Here we rely on the reflection rule to obtain the computational behaviour of the
% eliminator $\natrec$.

% This means for instance that we do not consider inductive types that would only
% make sense in ETT, but we deem this not to be a restriction and to the best of
% our knowledge is not something that is usually considered in the literature.
% %
% With that in mind, our translation features a global context of typed constants
% with the restriction that the types of those constants should be well-formed
% in ITT. Those constants are thus used as black boxes inside ETT.

% With this we are able to recover what we were missing from
% \Coq, without having to deal with the trouble of proving that the translation
% does not break the guard condition of fixed points, and we are instead relying on
% a more type-based approach.

\section{About Universes}
\labsec{elim-trans-sorts}

As I mentioned earlier in \nrefch{elim-reflection-framework}, sorts are treated
abstractly in the translation. In the formalisation sorts are defined using
the following class:
%
\begin{minted}{coq}
Class Sorts.notion := {
  sort : Type ;
  succ : sort -> sort ;
  prod_sort : sort -> sort -> sort ;
  sum_sort : sort -> sort -> sort ;
  eq_sort : sort -> sort ;
  eq_dec : forall s z : sort, {s = z} + {s <> z} ;
  succ_inj : forall s z, succ s = succ z -> s = z
}.
\end{minted}
%
From the notion of sorts, we require functions to get the sort of a sort,
the sort of a product from the sorts of its arguments, and the sort of an
identity type.
We also require some measure of decidable equality and injectivity on those.
From this we ensure the \acrshort{PTS} is \emph{functional} (in particular
without cumulativity) so that we have unique typing.

We are using classes here because they come with some automation in \Coq which
allows us to assume globally a notion of sort without having to specify it at
each use.

This allows us to instantiate this by a lot of different notions like a natural
number based hierarchy of \(\Type_i\) and even an extension of it with a
universe $\Prop$ of propositions as in \acrshort{CIC}.
We also provide an instance corresponding to \acrshort{2TT} as explained in
\nrefch{elim-hott}.

\marginnote[1.2cm]{
  For inconsistency of \(\Type\) \emph{in} \(\Type\), see \arefsubsec{coq-univ}.
}
In order to deal with examples in a simpler manner (making up for our lack of
universe polymorphism and cumulativity) by interacting with \Coq (thanks to
\MetaCoq), one of the instances we provide comes with only one universe $\Type$
and the inconsistent typing rule $\Type : \Type$.

\section{ETT-flavoured \Coq: Examples}
\labsec{ett-flavoured}

In this section I demonstrate how our translation can bring extensionality to
the world of \Coq in action. The examples can be found in
\rpath{plugin\_demo.v}.
Again, since we do not have \emph{weak} \Coq or \MetaCoq, these are in the case
of the translation to \acrshort{ITT} only.

\paragraph{First, a pedestrian approach.}
%
I would like to begin by showing how one can write an example step by step
before we show how it can be instrumented and automated as a plugin.
For this I use a self-contained example without any inductive
types or recursion, illustrating a very simple case of reflection.
The term we want to translate is the identity coercion:
\[
  \lambda\ A\ B\ e\ x.\ x : \Pi\ A\ B.\ A = B \to
  A \to B
\]
which relies on the equality \(e : A = B\) and reflection to convert  \(x : A\)
to \(x : B\).
%
Of course, this definition is not accepted in \Coq because this
conversion is not valid in \acrshort{ITT}.
%
\begin{minted}{coq}
Fail Definition pseudoid (A B : Type) (e : A = B) (x : A) : B
  := x.
\end{minted}
%
However, we still want to be able to write it \emph{in some way}, in order to
avoid manipulating de Bruijn indices directly.
\marginnote[0.1cm]{
  When I say ill-typed term, I mean a subterm that is typed but of the wrong
  type.
}%
For this, we use a little trick by first defining a \Coq axiom to represent
an ill-typed term:
%
\begin{minted}{coq}
Axiom candidate : forall A B (t : A), B.
\end{minted}
%
\mintinline{coq}|candidate A B t| is a candidate \mintinline{coq}|t| of type
\mintinline{coq}|A| to inhabit type \mintinline{coq}|B| in the fashion of
\ocaml's \mintinline{ocaml}|Obj.magic|.
We complete this by adding a notation that is reminiscent to \Agda's hole
mechanism.
%
\begin{minted}{coq}
Notation "'{!' t '!}'" := (candidate _ _ t).
\end{minted}

We can now write the \acrshort{ETT} function within \Coq.
%
\begin{minted}{coq}
Definition pseudoid (A B : Type) (e : A = B) (x : A) : B :=
  {! x !}.
\end{minted}
%
We can then quote the term and its type to \MetaCoq thanks to the
\mintinline{coq}|Quote Definition| command provided by the plugin.
%
\begin{minted}{coq}
Quote Definition pseudoid_term :=
  ltac:(let t := eval compute in pseudoid in exact t).
Quote Definition pseudoid_type :=
  ltac:(let T := type of pseudoid in exact T).
\end{minted}
\marginnote[-1.5cm]{
  The syntax is a bit heavy because of a call to \ltac used to quote the
  normal form of the term.
}
%
The terms that we get are now \MetaCoq terms, representing \Coq syntax.
We need to put them in \acrshort{ETT}, meaning adding the annotations, and also
removing the \mintinline{coq}|candidate| axiom.
This is the purpose of the \mintinline{coq}|fullquote| function that we provide
in our formalisation.
%
\marginnote[1cm]{
  \mintinline{coq}|fullquote| is given a \emph{big} number corresponding to the
  the number of recursive calls it is allowed to do. This \emph{fuel} technique
  allows us to circumvent the termination checker of \Coq.
}
\begin{minted}{coq}
Definition pretm_pseudoid :=
  Eval lazy in
  fullquote (2^18) Σ [] pseudoid_term empty empty nomap.

Definition tm_pseudoid :=
  Eval lazy in
  match pretm_pseudoid with
  | Success t => t
  | Error _ => sRel 0
  end.


Definition prety_pseudoid :=
  Eval lazy in
  fullquote (2^18) Σ [] pseudoid_type empty empty nomap.

Definition ty_pseudoid :=
  Eval lazy in
  match prety_pseudoid with
  | Success t => t
  | Error _ => sRel 0
  end.
\end{minted}
%
\mintinline{coq}|tm_pseudoid| and \mintinline{coq}|ty_pseudoid| correspond
respectively to the \acrshort{ETT} representation of \mintinline{coq}|pseudoid|
and its type.
We then produce, using our home-brewed \ltac type-checking tactic, the
corresponding \acrshort{ETT} typing derivation.
%
\begin{minted}{coq}
Lemma type_pseudoid : Σi ;;; [] |-x tm_pseudoid : ty_pseudoid.
Proof.
  unfold tm_pseudoid, ty_pseudoid.
  ettcheck. cbn.
  eapply reflection with (e := sRel 1).
  ettcheck.
Defined.
\end{minted}
%
Notice the use of \mintinline{coq}{reflection} which is a constructor of the
inductive type of typing derivations in \acrshort{ETT}. Its type is given by
\begin{minted}{coq}
reflection :
  forall A u v e,
    Σ ;;; Γ |-x e : sEq A u v ->
    Σ ;;; Γ |-x u ≡ v : A
\end{minted}
and corresponds to a use of the reflection rule.
We can then translate this derivation, obtain the translated term and then
convert it to \MetaCoq.
%
\begin{minted}{coq}
Definition itt_pseudoid : sterm :=
  Eval lazy in
  let '(_ ; t ; _) :=
    type_translation type_pseudoid istrans_nil
  in t.

Definition tc_pseudoid : tsl_result term :=
  Eval lazy in
  tsl_rec (2 ^ 18) Σ [] itt_pseudoid empty.
\end{minted}
%
Once we have it, we \emph{unquote} the term to obtain a \Coq term
(notice that the only use of reflection has been replaced by a transport).
%
\begin{minted}{coq}
fun (A B : Type) (e : A = B) (x : A) => transport e x
     : forall A B : Type, A = B -> A -> B
\end{minted}

\paragraph{Making a Plugin with \MetaCoq.}
%
All of this work is pretty systematic. Fortunately for us,
\MetaCoq also features a monad to reify \Coq commands which we can
use to \emph{program} the translation steps.
As such we have written a complete procedure, relying on type-checkers we
wrote for \acrshort{ITT} and \acrshort{ETT}, which can generate equality
obligations.

Thanks to this, the user does not have to know about the details of
implementation of the translation, and can stay within the \Coq ecosystem.

For instance, our previous example now becomes:
%
\begin{minted}{coq}
Definition pseudoid (A B : Type) (e : A = B) (x : A) : B :=
  {! x !}.

Run TemplateProgram (Translate ε "pseudoid").
\end{minted}
%
\marginnote[-0.6cm]{
  \mintinline{coq}{ε} is the empty translation context, see the next example to
  understand the need for a translation context
}
This produces a \Coq term \mintinline{coq}{pseudoid'} corresponding to the
translation.
Notice how the user does not even have to provide any proof of equality or
derivations of any sort. The derivation part is handled by our own typechecker
while the obligation part is solved automatically by the \Coq obligation mechanism.

\paragraph{About inductive types.}
%
As we promised, our translation is able to handle inductive types.
For this consider the inductive type of vectors (or length-indexed lists) below,
together with a simple definition (we will remain in \acrshort{ITT} for
simplicity).
%
\begin{minted}{coq}
Inductive vec A : nat -> Type :=
| vnil : vec A 0
| vcons : A -> forall n, vec A n -> vec A (S n).

Arguments vnil {_}.
Arguments vcons {_} _ _ _.

Definition vv := vcons 1 _ vnil.
\end{minted}
%
This time, in order to apply the translation we need to extend the translation
context with \mintinline{coq}|nat| and \mintinline{coq}|vec|.
%
\marginnote[1cm]{
  The \mintinline{coq}{_ <- _ ;; _} notation corresponds to a monadic bind.
}
\begin{minted}{coq}
Run TemplateProgram (
  Θ <- TranslateConstant ε "nat" ;;
  Θ <- TranslateConstant Θ "vec" ;;
  Translate Θ "vv"
).
\end{minted}
%
The command \mintinline{coq}|TranslateConstant| enriches the current
translation context with the types of the inductive type and of its
constructors. The translation context then also contains associative
tables between our own representation of constants and those of \Coq.
Unsurprisingly\sidenote{It is thanks to all the effort that has gone into
optimising the translation}, the translated \Coq term is the same as the
original term.

\paragraph{Reversal of vectors.}
%
Next, we tackle a motivating example: reversal on vectors.
Indeed, implementing this operation the same way it can
be done on lists ends up in the following conversion problem:
%
\begin{minted}{coq}
Fail Definition vrev {A n m} (v : vec A n) (acc : vec A m)
: vec A (n + m) :=
  vec_rect
    A (fun n _ => forall m, vec A m -> vec A (n + m))
    (fun m acc => acc)
    (fun a n _ rv m acc => rv _ (vcons a m acc))
    n v m acc.
\end{minted}
%
The recursive call returns a vector of length \mintinline{coq}|n + S m|
where the context expects one of length \mintinline{coq}|S n + m|. In
\acrshort{ITT}, these types are not convertible. This example is thus a perfect
fit for \acrshort{ETT} where we can use the fact that these two expressions
always compute to the same thing when instantiated with concrete numbers.
%
\begin{minted}{coq}
Definition vrev {A n m} (v : vec A n) (acc : vec A m)
: vec A (n + m) :=
  vec_rect
    A (fun n _ => forall m, vec A m -> vec A (n + m))
    (fun m acc => acc)
    (fun a n _ rv m acc => {! rv _ (vcons a m acc) !})
    n v m acc.

Run TemplateProgram (
  Θ <- TranslateConstant ε "nat" ;;
  Θ <- TranslateConstant Θ "vec" ;;
  Θ <- TranslateConstant Θ "Nat.add" ;;
  Θ <- TranslateConstant Θ "vec_rect" ;;
  Translate Θ "vrev"
).
\end{minted}
%
This generates four obligations that are all solved automatically. One of
them contains a proof of \mintinline{coq}|S n + m = n + S m| while the remaining
three correspond to the computation rules of addition (as mentioned before,
\mintinline{coq}|add| is simply a constant and does not compute in our
representation, hence the need for equalities).
%
The returned term is the following, with only one transport remaining
(remember our interpretation map removes unnecessary transports).
\begin{minted}{coq}
fun (A : Type) (n m : nat) (v : vec A n) (acc : vec A m) =>
vec_rect A
  (fun n _ => forall m, vec A m -> vec A (n + m))
  (fun m acc => acc)
  (fun a n₀ v₀ rv m₀ acc₀ =>
    transport
      (vrev_obligation_3 A n m v acc a n₀ v₀ rv m₀ acc₀)
      (rv (S m₀) (vcons a m₀ acc₀))
  ) n v m acc
: forall A n m, vec A n -> vec A m -> vec A (n + m)
\end{minted}

\section{Towards an Interfacing between \Andromeda and \Coq}

\Andromeda~\sidecite{andromeda} is a proof assistant implementing \acrshort{ETT}
in a sense that is really close to our formalisation. Aside from a concise
nucleus consisting in a very basic type theory, \Andromeda features an interface
in which the user can declare constants with given types. Definitions and
computational behaviour are defined using constants which inhabit equalities.

Here is for instance the definition of natural numbers and their eliminator
with computation rules.
\begin{minted}{text}
constant nat : Type
constant O : nat
constant S : nat -> nat

constant natrec :
  ∏ (P : nat -> Type),
    P O ->
    (∏ (n : nat), P n -> P (S n)) ->
    ∏ (n : nat), P n.

constant natrec_O :
  ∏ P Pz Ps, natrec P Pz Ps O ≡ Pz.

constant natrec_S :
  ∏ P Pz Ps n, natrec P Pz Ps (S n) ≡ Ps n (natrec P Pz Ps n).
\end{minted}
This is essentially what we do in our formalisation.
Furthermore, their theory relies on $\Type : \Type$, meaning, our modular
handling of universes can accommodate for this as well.

All in all, it should be possible in the future to use our translation
(or a similar one) to produce \Coq terms out of \Andromeda developments.
\Andromeda would be generating the typing derivations for us, which is
particularly interesting because \Andromeda's system is much more practical
than the small type-checker I wrote in \Coq.

\section{Composition with other Translations}

This translation also enables the formalisation of translations that
target \acrshort{ETT} rather than \acrshort{ITT} and still get mechanised proofs
of (relative) consistency by composition with this \acrshort{ETT} to
\acrshort{ITT} translation.
This could also be used to implement plugins based on the composition of
translations. In particular, supposing we have a theory which forms a
subset of \acrshort{ETT} and whose conversion is decidable. Using this
translation, we could formalise it as an embedded domain-specific type theory
and provide an automatic translation of well-typed terms into witnesses in
\Coq. This would make it possible to extend conversion with the theory
of lists for example.

This would provide a simple way to justify the consistency of
\acrshort{CoqMT}~\sidecite{DBLP:conf/lpar/JouannaudS17} for example, seeing it
as an extensional type theory where reflection is restricted to equalities on a
specific domain whose theory is decidable.